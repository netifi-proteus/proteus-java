plugins {
    id 'com.google.protobuf' version '0.8.5'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    protobuf project (':broker-info-idl')

    compile 'io.rsocket.rpc:core:0.1.2'
    compile 'io.rsocket.rpc:frames:0.1.2'
    compile project (':auth')
    compile project (':frames')

    compile 'com.typesafe:config:1.3.2'
    compile 'com.google.guava:guava:22.0'
    compile 'io.netty:netty-tcnative:2.0.12.Final:linux-x86_64'

    testCompile 'io.micrometer:micrometer-registry-atlas:1.0.3'
    testProtobuf 'io.rsocket.rpc:core-idl:0.1.2'
    testCompile 'io.rsocket.rpc:core:0.1.2'
    testCompile project(':tracing-openzipkin')
    testCompile project(':metrics-micrometer')
    testCompile 'javax.inject:javax.inject:1'
    testCompile 'junit:junit:4.12'
    testCompile 'org.apache.logging.log4j:log4j-api:2.9.0'
    testCompile 'org.apache.logging.log4j:log4j-core:2.9.0'
    testCompile 'org.apache.logging.log4j:log4j-slf4j-impl:2.9.0'
}

protobuf {
    generatedFilesBaseDir = "${projectDir}/src/generated"

    protoc {
        artifact = 'com.google.protobuf:protoc:3.6.0'
    }
    plugins {
        rsocketRpc {
            artifact = 'io.rsocket.rpc:protobuf-rpc:0.1.2'
        }
    }
    generateProtoTasks {
        all().each { task ->
            // Recompile protos when build.gradle has been changed, because
            // it's possible the version of protoc has been changed.
            task.inputs.file "${rootProject.projectDir}/build.gradle"
            task.plugins {
                rsocketRpc {}
            }
        }
    }
}

clean {
    delete protobuf.generatedFilesBaseDir
}

idea {
    module {
        sourceDirs += file("src/main/proto")
        sourceDirs += file("src/generated/main/java")
        sourceDirs += file("src/generated/main/proteus")

        generatedSourceDirs += file('src/generated/main/java')
        generatedSourceDirs += file('src/generated/main/proteus')
    }
}
