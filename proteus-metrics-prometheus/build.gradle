plugins {
    id 'com.google.protobuf' version '0.8.5'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile project (':proteus-client')
    compile 'io.rsocket.rpc:rsocket-rpc-protobuf:0.2.0'
    protobuf 'io.rsocket.rpc:rsocket-rpc-metrics-idl:0.2.0'
    compile 'io.micrometer:micrometer-registry-prometheus:1.0.3'

    testCompile 'org.apache.logging.log4j:log4j-api:2.9.0'
    testCompile 'org.apache.logging.log4j:log4j-core:2.9.0'
    testCompile 'org.apache.logging.log4j:log4j-slf4j-impl:2.9.0'
}

protobuf {
    generatedFilesBaseDir = "${projectDir}/src/generated"

    protoc {
        artifact = 'com.google.protobuf:protoc:3.6.0'
    }
    plugins {
        rsocketRpc {
            artifact = 'io.rsocket.rpc:rsocket-rpc-protobuf:0.2.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            // Recompile protos when build.gradle has been changed, because
            // it's possible the version of protoc has been changed.
            task.inputs.file "${rootProject.projectDir}/build.gradle"
            task.plugins {
                rsocketRpc {}
            }
        }
    }
}

clean {
    delete protobuf.generatedFilesBaseDir
}