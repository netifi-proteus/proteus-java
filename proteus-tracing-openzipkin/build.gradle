plugins {
    id 'com.google.protobuf' version '0.8.5'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile "io.rsocket.rpc:rsocket-rpc-protobuf:$rsocketRpcVersion"
    protobuf project (':proteus-tracing-idl')
    compile project (':proteus-client')

    compile 'io.projectreactor.addons:reactor-adapter:3.2.0.RELEASE'
    compile "com.google.protobuf:protobuf-java-util:$protobufVersion"

    compile 'io.opentracing:opentracing-api:0.31.0'
    compile 'io.opentracing.brave:brave-opentracing:0.31.2'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.4'
    compile group: 'com.hubspot.jackson', name: 'jackson-datatype-protobuf', version: '0.9.10-jackson2.9-proto3'

    testCompile 'org.apache.logging.log4j:log4j-api:2.9.0'
    testCompile 'org.apache.logging.log4j:log4j-core:2.9.0'
    testCompile 'org.apache.logging.log4j:log4j-slf4j-impl:2.9.0'
}

protobuf {
    generatedFilesBaseDir = "${projectDir}/src/generated"

    protoc {
        artifact = "com.google.protobuf:protoc:$protobufVersion"
    }
    plugins {
        rsocketRpc {
            artifact = "io.rsocket.rpc:rsocket-rpc-protobuf:$rsocketRpcVersion"
        }
    }
    generateProtoTasks {
        all().each { task ->
            // Recompile protos when build.gradle has been changed, because
            // it's possible the version of protoc has been changed.
            task.inputs.file "${rootProject.projectDir}/build.gradle"
            task.plugins {
                rsocketRpc {}
            }
        }
    }
}

clean {
    delete protobuf.generatedFilesBaseDir
}