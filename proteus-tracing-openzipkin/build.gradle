plugins {
    id 'com.google.protobuf' version '0.8.5'
}

description = 'Netifi Proteus Openzipkin Integration'

dependencies {
    protobuf project (':proteus-tracing-idl')
    compile project (':proteus-client')

    compile "io.rsocket.rpc:rsocket-rpc-protobuf"
    compile 'io.projectreactor.addons:reactor-adapter'
    compile "com.google.protobuf:protobuf-java-util"

    compile 'io.opentracing:opentracing-api'
    compile 'io.opentracing.brave:brave-opentracing'
    compile 'com.fasterxml.jackson.core:jackson-databind'
    compile 'com.hubspot.jackson:jackson-datatype-protobuf'
}

protobuf {
    generatedFilesBaseDir = "${projectDir}/src/generated"

    protoc {
        artifact = "com.google.protobuf:protoc:${ext['protobuf.version']}"
    }
    plugins {
        rsocketRpc {
            artifact = "io.rsocket.rpc:rsocket-rpc-protobuf"
        }
    }
    generateProtoTasks {
        all().each { task ->
            // Recompile protos when build.gradle has been changed, because
            // it's possible the version of protoc has been changed.
            task.inputs.file "${rootProject.projectDir}/build.gradle"
            task.plugins {
                rsocketRpc {}
            }
        }
    }
}

clean {
    delete protobuf.generatedFilesBaseDir
}